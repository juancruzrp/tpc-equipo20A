CREATE TRIGGER TR_ActualizarPrecioVenta_Producto
ON Productos
AFTER INSERT, UPDATE
AS
BEGIN
    SET NOCOUNT ON;

    -- Actualizar PrecioVenta solo para los productos afectados
    UPDATE p
    SET p.PrecioVenta = p.Precio + (p.Precio * pr.Porcentaje / 100.0)
    FROM Productos p
    INNER JOIN Proveedores pr ON p.IDProveedor = pr.IDProveedor
    INNER JOIN inserted i ON p.IDProducto = i.IDProducto    
END
GO

------------------------------------------------------

 CREATE TRIGGER TR_ActualizarPrecioVenta_Proveedor
ON Proveedores
AFTER UPDATE
AS
BEGIN
    SET NOCOUNT ON;

    -- Solo ejecutar si se modificó el Porcentaje
    IF UPDATE(Porcentaje)
    BEGIN
        UPDATE p
        SET p.PrecioVenta = p.Precio + (p.Precio * pr.Porcentaje / 100.0)
        FROM Productos p
        INNER JOIN Proveedores pr ON p.IDProveedor = pr.IDProveedor
        INNER JOIN inserted i ON pr.IDProveedor = i.IDProveedor        
    END
END
GO

------------------------------------------------------

CREATE TRIGGER TRG_ActualizarStock_Compra
ON Detalle_Compra
AFTER INSERT
AS
BEGIN
    SET NOCOUNT ON;

    -- Actualiza la tabla Productos
    -- Suma la Cantidad insertada al Stock actual
    UPDATE P
    SET P.Stock = P.Stock + I.Cantidad
    FROM Productos P
    INNER JOIN inserted I ON P.IDProducto = I.IDProducto;
END
GO

------------------------------------------------------

CREATE TRIGGER TR_ActualizarStock_Venta
ON Detalle_Venta
AFTER INSERT
AS
BEGIN
    SET NOCOUNT ON;

    -- Actualiza el stock de cada producto restando la cantidad vendida
    UPDATE P
    SET P.Stock = P.Stock - I.Cantidad
    FROM Productos P
    INNER JOIN inserted I
        ON P.IDProducto = I.IDProducto;
END